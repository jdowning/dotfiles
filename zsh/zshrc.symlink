export DF=$HOME/.dotfiles
export EDITOR=vim
if [[ -d $HOME/Dropbox ]]; then
  export HISTFILE=$HOME/Dropbox/macos/dotfiles/.zsh_history
fi

unalias run-help
autoload run-help
HELPDIR=/usr/local/share/zsh/help

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($DF/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# homebrew completions
if type brew &>/dev/null; then
  FPATH=$(brew --prefix)/share/zsh/site-functions:$FPATH
fi

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# dirs
DIRSTACKSIZE=8
setopt autopushd pushdminus pushdsilent pushdtohome
unsetopt nomatch

# added by Snowflake SnowCD installer
export PATH=/opt/snowflake/snowcd:$PATH

# added by Snowflake SnowSQL installer v1.2
export PATH=/Applications/SnowSQL.app/Contents/MacOS:$PATH

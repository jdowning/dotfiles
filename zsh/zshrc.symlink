export DF=$HOME/.dotfiles
export EDITOR=vim

unalias run-help
autoload run-help
HELPDIR=/usr/local/share/zsh/help

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($DF/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# dirs
DIRSTACKSIZE=8
setopt autopushd pushdminus pushdsilent pushdtohome
unsetopt nomatch

# color clock on the right
RPROMPT="${FG[117]}%D{%T %Z %m-%d-%Y}%{$reset_color%}"

# added by travis gem
[ -f /Users/justin/.travis/travis.sh ] && source /Users/justin/.travis/travis.sh
